buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

group = 'pl.pabilo8'
version = '0.1.0'
archivesBaseName = 'ctmb'

minecraft {
    version = project.forgeVersion
    runDir = "run"

    replace "@VERSION@", project.version

    mappings = project.mcpVersion

    useDepAts = true
}

repositories {
    mavenCentral()

    maven {
        name "Crafttweaker"
        url "https://maven.blamejared.com"
    }
    maven {
        name 'Tinkers/JEI'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Chickens'
        url 'https://minecraft.curseforge.com/api/maven/'
    }

    maven { // ModMaven, there is a bunch of stuff on there
        name 'ModMaven'
        url "https://modmaven.k-4u.nl/"
    }

    //https://www.cursemaven.com/
    //for Immersive Petroleum and Intelligence
    maven {
        url "https://www.cursemaven.com"
    }
}

// change default maven url of FG2 to the new one.
repositories.each {
    if (it instanceof MavenArtifactRepository && it.url.toString() == "https://files.minecraftforge.net/maven") {
        it.url = "https://maven.minecraftforge.net/"
    }
}


dependencies {
    // compile against the good ol' 92
    deobfCompile "blusunrize:ImmersiveEngineering:0.12-92-+"
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.675"

    compile "mezz.jei:jei_1.12.2:4.16.1.302"
    //compile "CraftTweaker2:CraftTweaker2-MC1120-Mod-JEI:1.12-4.1.20.675"

    compileOnly "curse.maven:immersive-petroleum-268250:2544919"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

def libPrefix = 'META-INF/libraries'

jar {
    manifest {
        attributes "FMLAT": "ctmb_at.cfg"
        attributes 'Maven-Artifact': group + ':' + archivesBaseName + ':' + version
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLAT': 'ctmb_at.cfg'
        attributes 'Maven-Artifact': group + ':' + archivesBaseName + ':' + version
    }
}